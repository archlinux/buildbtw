set dotenv-load := true
set working-directory := '..'

run-server *args: create-db
    cargo run --bin server -- run {{ args }}

watch-server *args: create-db
    systemfd --no-pid -s http::8080 -- cargo watch -w buildbtw-poc/src -w buildbtw-poc/templates -w Cargo.toml -w buildbtw-poc/Cargo.toml -- just --justfile buildbtw-poc/Justfile run-server {{ args }}

run-client *args:
    cargo run --bin client -- {{ args }}

watch-client *args:
    cargo watch -w buildbtw-poc/src -w buildbtw-poc/templates -w buildbtw-poc/Cargo.toml -w Cargo.toml -- just --justfile buildbtw-poc/Justfile run-client {{ args }}

run-worker *args:
    cargo run --bin worker -- run {{ args }}

run-worker-fake *args:
    cargo run --bin worker --features fake-pkgbuild -- run {{ args }}

# TODO `cargo watch` interferes with stdin handling,
# so the worker can't ask for a password to
# use sudo :/
watch-worker *args:
    cargo watch -w buildbtw-poc/src -w buildbtw-poc/templates -w buildbtw-poc/Cargo.toml -w Cargo.toml -- just --justfile buildbtw-poc/Justfile run-worker {{ args }}

clean:
    rm -rf buildbtw-poc/source_repos
    rm -rf buildbtw-poc/build
    cargo clean -p buildbtw-poc

test *args:
    cargo test --package buildbtw-poc {{ args }}

watch-test *args:
    cargo watch -w buildbtw-poc/src -w buildbtw-poc/templates -w buildbtw-poc/Cargo.toml -w Cargo.toml -- just --justfile buildbtw-poc/Justfile test {{ args }}

update-graphql-schema:
    graphql-client introspect-schema "https://$GITLAB_DOMAIN/api/graphql" --authorization "$GITLAB_TOKEN" --output buildbtw-poc/src/gitlab/gitlab_schema.json

lint *args:
    cargo clippy --package buildbtw-poc --all-targets {{args}} -- -D warnings
    cargo fmt --package buildbtw-poc -- --check

lint-fix:
    just lint --package buildbtw-poc --fix --allow-staged
    cargo fmt --package buildbtw-poc

create-db: && migrate-db
    sqlx db create

migrate-db:
    sqlx migrate run --source buildbtw-poc/migrations

reset-db: && create-db
    sqlx db drop
